


```
prama solidity ^0.4.16;

contract Ballot{

	/// 投票人
	struct Voter{
		uint weight;	// 表示拥有的票数
		bool voted;	// 是否已经投过票
		address delegate;	// 把投票权力委托给别人
		uint vote;	// 投的选项 id
	}

	/// 投票的 一个选项
	struct Proposal{
		bytes32 name;
		uint voteCount;
	}


	/// 创建本次投票的人
	address public chairperson;

	/// eth 账号 到 投票人的映射，一个 eth 账号 表示 一个投票人
	mapping(address => Voter) public voters;

	/// 所有选项数组
	Proposal[] public proposals;

	function Ballot(bytes32[] proposalNames) public{
		chairperson = msg.sender;
		voters[chairperson].weight = 1;

		/// 初始化 所有的投票选项
		for(uint i=0; i<proposalNames.length; i++){
			proposals.push(Proposal({name: proposalNames[i], voteCount: 0}));
		}
	}

	/// 投票创建人 chairperson 赋予 某个人 投票的权利
	function giveRightToVote(address voter) public {
		require((msg.sender == chairperson) && 
			!voters[voter].voted &&
			(voters[voter].weight == 0));

			voters[voter].weight = 1;
	}

	/// 把投票权委托给某个人
	function delegate(address to) public {
		Voter storage sender = voters[msg.sender];
		require(!sender.voted);
		require(to != msg.sender);

		/// 保证委托人有投票权
		while(voters[to].delegate != address(0)){
			to = voters[to].delegate;

			require(to != msg.sender);
		}

		sender.voted = true;
		sender.delegate = to;
		Voter storage delegate_ = voters[to];
		if(delegate_.voted){
			/// 如果被委托的人，已经投过票，直接增加选项的得票数
			proposals[delegate_.vote].voteCount += sender.weight;
		}else{
			/// 如果被委托的人，没有投票，则增加他的票数
			delegate_.weight += sender.weight;
		}
	}

	function vote(uint proposal) public {
		Voter storage sender = voters[msg.sender];
		require(!sender.voted);

		sender.voted = true;
		sender.vote = proposal;
		proposals[proposal].voteCount += sender.weight;
	}

	function winningProposal() public view returns(uint winningProposal_){

		uint winningVoteCount = 0;
        for (uint p = 0; p < proposals.length; p++) {
            if (proposals[p].voteCount > winningVoteCount) {
                winningVoteCount = proposals[p].voteCount;
                winningProposal_ = p;
            }
        }
	}

	function winnerName() public view returns(bytes32 winnerName_){
		winnerName_ = proposals[winningProposal()].name;
	}

}
```

# 需要改进的地方

给某个账户添加投票权，需要很多交易。

